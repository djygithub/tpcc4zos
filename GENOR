//GENOR    JOB (330DJY00),'D J YOUNG ',MSGCLASS=Y,NOTIFY=DJY00          00001008
//COB2UCL PROC                                                          00010000
//*   PROC FOR COBOL II - COMPILE AND LINK                              00020000
//COB2 EXEC PGM=IGYCRCTL,PARM='OBJECT,LIB,APOST,LIST',REGION=1024K      00030002
//*TEPLIB  DD  DSNAME=DB2IMS.COB2COMP,DISP=SHR                          00040005
//STEPLIB  DD  DSNAME=SYS1.COB2COMP,DISP=SHR                            00040005
//SYSPRINT DD  SYSOUT=*                                                 00050000
//SYSLIN   DD  DSNAME=&&LOADSET,UNIT=3390,DISP=(MOD,PASS),              00060000
//             SPACE=(TRK,(3,3))                                        00070000
//SYSUT1   DD  UNIT=3390,SPACE=(CYL,(1,1))                              00080000
//SYSUT2   DD  UNIT=3390,SPACE=(CYL,(1,1))                              00090000
//SYSUT3   DD  UNIT=3390,SPACE=(CYL,(1,1))                              00100000
//SYSUT4   DD  UNIT=3390,SPACE=(CYL,(1,1))                              00110000
//SYSUT5   DD  UNIT=3390,SPACE=(CYL,(1,1))                              00120000
//SYSUT6   DD  UNIT=3390,SPACE=(CYL,(1,1))                              00121000
//SYSUT7   DD  UNIT=3390,SPACE=(CYL,(1,1))                              00122000
//LKED   EXEC  PGM=IEWL,PARM='LIST,XREF,',COND=(5,LT,COB2),             00130003
//             REGION=512K                                              00140003
//SYSLIN   DD  DSNAME=&&LOADSET,DISP=(OLD,DELETE)                       00150003
//SYSLMOD  DD  DSNAME=DJY00.RAND.PGMLIB(GENOR),                         00170005
//             DISP=SHR                                                 00180003
//SYSLIB   DD  DSNAME=SYS1.COB2LIB,DISP=SHR                             00190005
//         DD  DSNAME=CEE.V1R5M0.SCEELKED,DISP=SHR                      00190005
//         DD  DSNAME=DJY00.RAND.SRCLIB,DISP=SHR                        00190005
//SYSUT1   DD  UNIT=3390,SPACE=(CYL,(1,1))                              00200003
//SYSPRINT DD  SYSOUT=*                                                 00210003
// PEND                                                                 00220000
//DOIT EXEC COB2UCL                                                     00230000
//SYSIN DD *                                                            00240010
       ID DIVISION.                                                             
       PROGRAM-ID. GENOR.                                                       
       AUTHOR. D J YOUNG.                                                       
       ENVIRONMENT DIVISION.                                                    
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT OUTPUT-FILE ASSIGN TO UT-S-OUTPUT.                            
           SELECT OUTPUT-FILE-2 ASSIGN TO UT-S-OUTPUT2.                         
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  OUTPUT-FILE                                                          
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 82 CHARACTERS                                        
           RECORDING MODE F                                                     
           LABEL RECORDS ARE STANDARD.                                          
       01  OUTPUT-RECORD.                                                       
           05 FILLER            PIC X(82).                                      
       FD  OUTPUT-FILE-2                                                        
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 115 CHARACTERS                                       
           RECORDING MODE F                                                     
           LABEL RECORDS ARE STANDARD.                                          
       01  OUTPUT-RECORD-2.                                                     
           05 FILLER            PIC X(115).                                     
       WORKING-STORAGE SECTION.                                                 
       77  YES-FLAG             PIC X VALUE 'Y'.                                
       77  NO-FLAG              PIC X VALUE 'N'.                                
       77  ORDERLINE-COUNTER    PIC S9(8) COMP.                                 
       01  OUTPUT-BUFFER.                                                       
           05  O-ID              PIC 9(8).                                      
           05  O-D-ID            PIC 9(8).                                      
           05  O-W-ID            PIC 9(8).                                      
           05  O-C-ID            PIC 9(8).                                      
           05  O-ENTRY-D.                                                       
               07  FILLER        PIC 9(2) VALUE 19.                             
               07  DEEDATE-1     PIC 9(2).                                      
               07  FILLER        PIC X VALUE '-'.                               
               07  DEEDATE-2     PIC 9(2).                                      
               07  FILLER        PIC X VALUE '-'.                               
               07  DEEDATE-3     PIC 9(2).                                      
               07  FILLER        PIC X VALUE '-'.                               
               07  DEETIME-HOUR  PIC 9(2).                                      
               07  FILLER        PIC X VALUE '.'.                               
               07  DEETIME-MIN   PIC 9(2).                                      
               07  FILLER        PIC X VALUE '.'.                               
               07  DEETIME-SECS  PIC 9(2).                                      
               07  FILLER        PIC X VALUE '.'.                               
               07  DEETIME-HSEC  PIC 9(2).                                      
               07  FILLER        PIC 9(4) VALUE 0.                              
           05  O-CARRIER-ID      PIC 9(8).                                      
           05  O-OL-CNT          PIC 9(8).                                      
           05  O-ALL-LOCAL       PIC 9(8) VALUE 1.                              
       01  OUTPUT-BUFFER-2.                                                     
           05  OL-O-ID           PIC 9(8).                                      
           05  OL-D-ID           PIC 9(8).                                      
           05  OL-W-ID           PIC 9(8).                                      
           05  OL-NUMBER         PIC 9(8).                                      
           05  OL-I-ID           PIC 9(8).                                      
           05  OL-SUPPLY-W-ID    PIC 9(8).                                      
           05  OL-DELIVERY-D.                                                   
               07  FILLER        PIC 9(2) VALUE 19.                             
               07  DEEDATE-1     PIC 9(2).                                      
               07  FILLER        PIC X VALUE '-'.                               
               07  DEEDATE-2     PIC 9(2).                                      
               07  FILLER        PIC X VALUE '-'.                               
               07  DEEDATE-3     PIC 9(2).                                      
               07  FILLER        PIC X VALUE '-'.                               
               07  DEETIME-HOUR  PIC 9(2).                                      
               07  FILLER        PIC X VALUE '.'.                               
               07  DEETIME-MIN   PIC 9(2).                                      
               07  FILLER        PIC X VALUE '.'.                               
               07  DEETIME-SECS  PIC 9(2).                                      
               07  FILLER        PIC X VALUE '.'.                               
               07  DEETIME-HSEC  PIC 9(2).                                      
               07  FILLER        PIC 9(4) VALUE 0.                              
           05  OL-QUANTITY       PIC 9(8).                                      
           05  OL-AMOUNT         PIC 9(6).                                      
           05  OL-AMOUNT-F       PIC X(3) VALUE 'E-2'.                          
           05  OL-DIST-INFO      PIC X(24).                                     
       01  DEE-TIME-STAMP.                                                      
           05  DEEDATE.                                                         
               07  DEEDATE-1           PIC 9(2).                                
               07  DEEDATE-2           PIC 9(2).                                
               07  DEEDATE-3           PIC 9(2).                                
           05  DEETIME.                                                         
               07  DEETIME-HOUR        PIC 9(2).                                
               07  DEETIME-MIN         PIC 9(2).                                
               07  DEETIME-SECS        PIC 9(2).                                
               07  DEETIME-HSEC        PIC 9(2).                                
           05  O-WORK-D.                                                        
               07  FILLER        PIC 9(2) VALUE 19.                             
               07  DEEDATE-1     PIC 9(2) VALUE 55.                             
               07  FILLER        PIC X VALUE '-'.                               
               07  DEEDATE-2     PIC 9(2) VALUE 5.                              
               07  FILLER        PIC X VALUE '-'.                               
               07  DEEDATE-3     PIC 9(2) VALUE 6.                              
               07  FILLER        PIC X VALUE '-'.                               
               07  DEETIME-HOUR  PIC 9(2) VALUE 12.                             
               07  FILLER        PIC X VALUE '.'.                               
               07  DEETIME-MIN   PIC 9(2) VALUE 12.                             
               07  FILLER        PIC X VALUE '.'.                               
               07  DEETIME-SECS  PIC 9(2) VALUE 12.                             
               07  FILLER        PIC X VALUE '.'.                               
               07  DEETIME-HSEC  PIC 9(2) VALUE 12.                             
               07  FILLER        PIC 9(4) VALUE 0.                              
       01  RANDOM-TABLE-24.                                                     
           03 RANDOM-STRINGS-24.                                                
            05  FILLER PIC X(24) VALUE '1234567890-=QWERTYUI1234'.              
            05  FILLER PIC X(24) VALUE 'QWERTYUIOP›\ASDFGHJK13RK'.              
            05  FILLER PIC X(24) VALUE 'ASDFGHJKL; {ZXCVBNM,Q4TK'.              
            05  FILLER PIC X(24) VALUE '<ZXCVBNM,./|LKJHGTFRARYK'.              
            05  FILLER PIC X(24) VALUE '@#$%^&*()_+!UJNHTGFRZFLU'.              
            05  FILLER PIC X(24) VALUE 'º:"}>,.?1234FRDESWAQXVLI'.              
            05  FILLER PIC X(24) VALUE '567890-=QWERSXDCFVGBSBLO'.              
            05  FILLER PIC X(24) VALUE 'TYUIOP›\ASDFFRGTHYJUWGLP'.              
            05  FILLER PIC X(24) VALUE 'GHJKL; {<ZXCNHMJ,K.L2TLL'.              
            05  FILLER PIC X(24) VALUE 'VBNM,./|@#$%CDXSZASW3TLK'.              
            05  FILLER PIC X(24) VALUE '^&*()_+!º:"}CDVFBGNHEHLJ'.              
           03 RANDOM-THINGS-24 REDEFINES RANDOM-STRINGS-24                      
                                 OCCURS 11 TIMES                                
                                 PIC X(24).                                     
       01  RANDASM-PARMS.                                                       
           05  LOWER-BOUND      PIC S9(9) COMP VALUE 1.                         
           05  UPPER-BOUND      PIC S9(9) COMP VALUE 200.                       
           05  RANGE2           PIC S9(9) COMP VALUE 404.                       
           05  SEED             PIC S9(9) COMP.                                 
           05  O-C-ID-WORK      PIC S9(9) COMP.                                 
           05  O-CARRIER-WORK   PIC S9(9) COMP.                                 
           05  O-OL-CNT-WORK    PIC S9(9) COMP.                                 
           05  OL-ID-WORK       PIC S9(9) COMP.                                 
           05  OL-AMOUNT-WORK   PIC S9(9) COMP.                                 
           05  OL-DIST-WORK     PIC S9(9) COMP.                                 
       LINKAGE SECTION.                                                         
       01  INPUT-PARMS.                                                         
           05  PARM-LENGTH      PIC S9(3) COMP.                                 
           05  WAREHOUSE-NUM-B  PIC 9(9).                                       
           05  FILLER           PIC X.                                          
           05  WAREHOUSE-NUM-E  PIC 9(9).                                       
           05  FILLER           PIC X.                                          
           05  TOTAL-ROWS       PIC 9(9).                                       
       PROCEDURE DIVISION USING INPUT-PARMS.                                    
       MAIN-ROUTINE.                                                            
           ACCEPT DEETIME FROM TIME.                                            
           DISPLAY DEETIME.                                                     
           ACCEPT DEEDATE FROM DATE.                                            
           DISPLAY DEEDATE.                                                     
           MOVE CORRESPONDING DEETIME TO O-ENTRY-D.                             
           MOVE CORRESPONDING DEEDATE TO O-ENTRY-D.                             
           DISPLAY O-ENTRY-D.                                                   
      *(1)                                                                      
           OPEN OUTPUT OUTPUT-FILE                                              
                       OUTPUT-FILE-2.                                           
      *(2)                                                                      
           MOVE 1 TO SEED.                                                      
      *(3)                                                                      
           MOVE 1 TO LOWER-BOUND.                                               
           MOVE 3000 TO UPPER-BOUND.                                            
           COMPUTE RANGE2 = 2 * (UPPER-BOUND - LOWER-BOUND + 1).                
           CALL 'RANDAS01' USING YES-FLAG,                                      
                                 O-C-ID-WORK,                                   
                                 SEED,                                          
                                 LOWER-BOUND,                                   
                                 UPPER-BOUND,                                   
                                 RANGE2.                                        
           MOVE 1 TO LOWER-BOUND.                                               
           MOVE 10 TO UPPER-BOUND.                                              
           COMPUTE RANGE2 = 2 * (UPPER-BOUND - LOWER-BOUND + 1).                
           CALL 'RANDAS02' USING YES-FLAG,                                      
                                 O-CARRIER-WORK,                                
                                 SEED,                                          
                                 LOWER-BOUND,                                   
                                 UPPER-BOUND,                                   
                                 RANGE2.                                        
           MOVE 5 TO LOWER-BOUND.                                               
           MOVE 15 TO UPPER-BOUND.                                              
           COMPUTE RANGE2 = 2 * (UPPER-BOUND - LOWER-BOUND + 1).                
           CALL 'RANDAS03' USING YES-FLAG,                                      
                                 O-OL-CNT-WORK,                                 
                                 SEED,                                          
                                 LOWER-BOUND,                                   
                                 UPPER-BOUND,                                   
                                 RANGE2.                                        
           MOVE 1 TO LOWER-BOUND.                                               
           MOVE 100000 TO UPPER-BOUND.                                          
           COMPUTE RANGE2 = 2 * (UPPER-BOUND - LOWER-BOUND + 1).                
           CALL 'RANDAS04' USING YES-FLAG,                                      
                                 OL-ID-WORK,                                    
                                 SEED,                                          
                                 LOWER-BOUND,                                   
                                 UPPER-BOUND,                                   
                                 RANGE2.                                        
           MOVE 1 TO LOWER-BOUND.                                               
           MOVE 999999 TO UPPER-BOUND.                                          
           COMPUTE RANGE2 = 2 * (UPPER-BOUND - LOWER-BOUND + 1).                
           CALL 'RANDAS05' USING YES-FLAG,                                      
                                 OL-AMOUNT-WORK,                                
                                 SEED,                                          
                                 LOWER-BOUND,                                   
                                 UPPER-BOUND,                                   
                                 RANGE2.                                        
           MOVE 1 TO LOWER-BOUND.                                               
           MOVE 11 TO UPPER-BOUND.                                              
           COMPUTE RANGE2 = 2 * (UPPER-BOUND - LOWER-BOUND + 1).                
           CALL 'RANDAS06' USING YES-FLAG,                                      
                                 OL-DIST-WORK,                                  
                                 SEED,                                          
                                 LOWER-BOUND,                                   
                                 UPPER-BOUND,                                   
                                 RANGE2.                                        
      *(4)                                                                      
           PERFORM COMPUTE-AND-WRITE-WH UNTIL WAREHOUSE-NUM-B                   
               GREATER THAN WAREHOUSE-NUM-E.                                    
      *(5)                                                                      
           CLOSE OUTPUT-FILE                                                    
                 OUTPUT-FILE-2.                                                 
           GOBACK.                                                              
      ******************************************************************        
      *                                                                         
      *                                                                         
      ******************************************************************        
       COMPUTE-AND-WRITE-WH.                                                    
           MOVE WAREHOUSE-NUM-B TO O-W-ID.                                      
           MOVE 1 TO O-D-ID.                                                    
           PERFORM COMPUTE-AND-WRITE-DI UNTIL O-D-ID > 10.                      
           ADD 1 TO WAREHOUSE-NUM-B.                                            
       COMPUTE-AND-WRITE-DI.                                                    
           MOVE 1 TO O-ID.                                                      
           PERFORM COMPUTE-AND-WRITE-OR UNTIL O-ID > 3000.                      
           ADD 1 TO O-D-ID.                                                     
       COMPUTE-AND-WRITE-OR.                                                    
           CALL 'RANDAS01' USING NO-FLAG,                                       
                                O-C-ID-WORK.                                    
           MOVE O-C-ID-WORK TO O-C-ID.                                          
           IF  O-C-ID < 2101                                                    
               CALL 'RANDAS02' USING NO-FLAG,                                   
                                    O-CARRIER-WORK                              
               MOVE O-CARRIER-WORK TO O-CARRIER-ID                              
           ELSE                                                                 
               MOVE 0 TO O-CARRIER-ID.                                          
           CALL 'RANDAS03' USING NO-FLAG,                                       
                                O-OL-CNT-WORK.                                  
           MOVE O-OL-CNT-WORK TO O-OL-CNT.                                      
           WRITE OUTPUT-RECORD FROM OUTPUT-BUFFER.                              
           MOVE O-ID TO OL-O-ID.                                                
           MOVE O-D-ID TO OL-D-ID.                                              
           MOVE O-W-ID TO OL-W-ID.                                              
           MOVE O-W-ID TO OL-SUPPLY-W-ID.                                       
           MOVE 5 TO OL-QUANTITY.                                               
           IF  OL-O-ID < 2101                                                   
               MOVE O-ENTRY-D TO OL-DELIVERY-D                                  
           ELSE                                                                 
               MOVE O-WORK-D TO OL-DELIVERY-D.                                  
           MOVE 1 TO ORDERLINE-COUNTER.                                         
           PERFORM COMPUTE-AND-WRITE-OL UNTIL ORDERLINE-COUNTER                 
               GREATER THAN O-OL-CNT.                                           
           ADD 1 TO O-ID.                                                       
       COMPUTE-AND-WRITE-OL.                                                    
           MOVE ORDERLINE-COUNTER TO OL-NUMBER.                                 
           CALL 'RANDAS04' USING NO-FLAG,                                       
                               OL-ID-WORK.                                      
           MOVE OL-ID-WORK TO OL-I-ID.                                          
           CALL 'RANDAS05' USING NO-FLAG,                                       
                               OL-AMOUNT-WORK.                                  
           MOVE OL-AMOUNT-WORK TO OL-AMOUNT.                                    
           CALL 'RANDAS06' USING NO-FLAG,                                       
                               OL-DIST-WORK.                                    
           MOVE RANDOM-THINGS-24(OL-DIST-WORK) TO OL-DIST-INFO.                 
           WRITE OUTPUT-RECORD-2 FROM OUTPUT-BUFFER-2.                          
           ADD 1 TO ORDERLINE-COUNTER.                                          
